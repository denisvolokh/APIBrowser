<?xml version="1.0" encoding="utf-8"?>
<s:Form xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark" 
		xmlns:mx="library://ns.adobe.com/flex/mx" 
		width="100%" height="100%" 
		xmlns:components="com.elanceapibrowser.components.*">
	
	<fx:Script>
		<![CDATA[
			import com.elanceapibrowser.model.Method;
			import com.elanceapibrowser.model.MethodParam;
			
			private var _method : Method;
			
			public function get method():Method
			{
				return _method;
			}

			public function set method(value:Method):void
			{
				_method = value;
				
				renderParams();
			}
			
			private function renderParams():void
			{
				removeAllElements();
				
				for each (var item : MethodParam in method.queryParams)
				{
					var param : QueryItemRenderer = new QueryItemRenderer;
					param.paramName = item.name;
					addElement(param);
				}
			}
			
			public function getQueryParams():Array
			{
				var params : Array = [];
				
				for (var i : int = 0; i < this.numElements; i++)
				{
					var item : QueryItemRenderer = this.getElementAt(i) as QueryItemRenderer;
					var param : MethodParam = new MethodParam();
					param.name = (item as QueryItemRenderer).paramName;
					param.value = (item as QueryItemRenderer).inputParam.text;
					params.push(param);
				}
				
				return params;
			}
			
			public function getQueryString():String
			{
				var str : String = "";
				
				for (var i : int = 0; i < this.numElements; i++)
				{
					var item : QueryItemRenderer = this.getElementAt(i) as QueryItemRenderer;
					
					if (item.isRequired || item.inputParam.text != "")
					{
						if (i == 0) str = "?";
						str += item.paramName;
						str	+= "="; 
						str += item.inputParam.text; 
						str += (i != this.numElements - 1)?"&":"";								
					}
				}
				
				return str;
			}
			
			public function validateQueryBuilder():Boolean
			{
				for (var i : int = 0; i < this.numElements; i++)
				{
					var item : QueryItemRenderer = this.getElementAt(i) as QueryItemRenderer;
					if (item.validateParameter() == false)
					{
						return false;
					}
				}
				
				return true;
			}

		]]>
	</fx:Script>
</s:Form>
